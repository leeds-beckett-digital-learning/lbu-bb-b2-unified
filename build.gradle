buildscript {  
    repositories {  
        jcenter()  
    }  
  
  
    dependencies {  
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.2.5'  
    }  
}  
  
  
apply plugin: 'com.bmuschko.tomcat' 
apply plugin: "java"
apply plugin: "war"
apply plugin: 'maven-publish'

// define the version for the project when publishing to maven
group "uk.ac.leedsbeckett"
version "1.2.1"
description = 'LBU-BB-B2-Unified'

repositories {
  jcenter()
  maven {
    url "https://maven.blackboard.com/repository/releases/"
  }
    maven {
      url "https://maven.pkg.github.com/leeds-beckett-digital-learning/lbu-bb-b2-utils"
          credentials {
              username = gitHubUserName
              password = gitHubPrivateToken // the variable resides in ~/.gradle/gradle.properties
          }
    }
}

configurations {
  buildUtils
}

tomcat {
    jasper {
        validateXml = true
        webXmlFragment = file("$webAppDir/WEB-INF/generated_web.xml")
        addWebXmlMappings = true
    }
}

// define the project's dependencies
dependencies {

  def tomcatVersion = '8.0.42'  
  tomcat  "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",  
          "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}",  
          "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}",
          "blackboard.platform:bb-platform:3900.13.0",
          "blackboard.platform:bb-taglibs:3900.13.0",
          "javax.servlet:jstl:1.2",
          "com.fasterxml.jackson.core:jackson-databind:2.10.0",
          "net.sf.json-lib:json-lib:2.2.3:jdk15"
  

  compileOnly( "log4j:log4j:1.2.17" )
  compileOnly( "javax.servlet:javax.servlet-api:3.1.0" )
  compileOnly( "com.sun.jersey:jersey-server:1.19.4" )
  compileOnly( "com.sun.jersey:jersey-core:1.19.4" )
  compileOnly( "com.sun.jersey:jersey-servlet:1.19.4" )
  compileOnly( "com.sun.jersey:jersey-json:1.19.4" )
  compileOnly( "javax.cache:cache-api:1.1.0" )
  compileOnly( "javax.jms:jms:1.1" )
  compileOnly "com.sun.mail:javax.mail:1.6.2"
  compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
  
  // Dependencies are libraries needed to build, but should not be included in the B2 WAR.
  // You should NEVER include Learn JARs (other than webapis) in your B2.
  compileOnly( "blackboard.platform:bb-platform:3900.13.0" ) { transitive = false }
  compileOnly( "blackboard.platform:bb-cms-admin:3900.13.0" ) { transitive = false }
  compileOnly( "blackboard.platform:bb-taglibs:3900.13.0" ) { transitive = false }

  implementation 'org.apache.commons:commons-text:1.9'
  implementation 'uk.ac.leedsbeckett:bbb2utils:0.2.11'  
}

task compileJsps(type: JavaCompile) {
  group = 'build'
  description = 'Translates and compiles JSPs'
  classpath = configurations.tomcat + sourceSets.main.output + sourceSets.main.runtimeClasspath
  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'
  destinationDir = file("src/main/webapp/WEB-INF/classes")
  source = file("build/jasper/org/apache/jsp")
  dependsOn classes
  dependsOn tomcatJasper
}

war.dependsOn compileJsps


publishing {
    publications {
        library(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url "https://maven.pkg.github.com/leeds-beckett-digital-learning/lbu-bb-b2-unified"
            credentials {
                username = gitHubUserName
                password = gitHubPrivateToken // the variable resides in ~/.gradle/gradle.properties
            }
        }
    }
}
